// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext.buildConfig = [
            'compileSdk': 27,
            'minSdk'    : 14,
            'targetSdk' : 27,

            'version'   : [
                    'major': 0,
                    'minor': 0,
                    'patch': 4,
                    'build': 0,
            ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
            'gradle'        : '3.1.2',
            'kotlin'        : '1.2.31',
            'supportLibrary': '27.1.1'
    ]

    ext.deps = [
            'kotlin'     : "org.jetbrains.kotlin:kotlin-stdlib-jre7:${versions.kotlin}",
            'android'    : [
                    'support'    : [
                            'appcompat': "com.android.support:appcompat-v7:${versions.supportLibrary}"
                    ],
                    'databinding': [
                            'compiler': "com.android.databinding:compiler:${versions.gradle}",
                            'library' : "com.android.databinding:library:${versions.gradle}"
                    ]
            ],
            'junit'      : 'junit:junit:4.12',
            'assertj'    : 'org.assertj:assertj-core:3.9.1',
            'mvplib'     : 'com.kvazars:mvp:3.4.0',
            'preferences': project(":library"),
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
